plugins {
    id 'application'
}

sourceCompatibility = 1.8
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

dependencies {
    implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

    implementation files('./libs/tween-engine-api.jar')

    implementation project(':loader-libgdx')

}

sourceSets {
    main {
        java.srcDirs = ["src/main/java"]
        resources.srcDirs = ["src/main/resources"]
    }
}

application {
    mainClass = 'aurelienribon.bodyeditor.Main'
}

tasks.withType(Exec) {
    group = 'loader-demo'
}

jar {
    archiveFileName = "loader-libgdx-demo.jar"
}

eclipse.project {
    name = appName + "-loader-libgdx-demo"
}

// Mark the resources dir in IntelliJ as an actual resource dir, not another java source dir
// Derived from https://discuss.gradle.org/t/13391/2 but needs a TODO for Gradle import style vs XML hacking
idea {
    module.iml.withXml {
        def node = it.asNode()
        def content = node.component.find { it.'@name' == 'NewModuleRootManager' }.content[0]
        content.sourceFolder.each { sourceFolder ->
            if (sourceFolder.@url?.endsWith('/resources')) {
                sourceFolder.attributes().with {
                    boolean isTestSource = (remove('isTestSource') == 'true')
                    put('type', isTestSource ? 'java-test-resource' : 'java-resource')
                }
            }
        }
    }
}
